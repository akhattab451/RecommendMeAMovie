plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
    id "com.google.protobuf" version "0.8.12"

}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        useIR = true
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.recommendmeamovie"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        buildConfigField ("String", "API_KEY", tmdbAPIKey)
        testInstrumentationRunner "com.example.recommendmeamovie.HiltTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    kapt {
        correctErrorTypes true
    }
    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
    testOptions.unitTests {
        returnDefaultValues = true
        includeAndroidResources = true
    }
}

dependencies {

    // Kotlin
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"

    // Support Libraries
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // LiveData and ViewModel
    def lifecycle = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation"

    // Retrofit
    def retrofit = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    // Room
    def room = "2.3.0"
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"

    // Coroutines
    def coroutines = "1.4.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_ver"
    kapt "com.google.dagger:hilt-compiler:$hilt_ver"

    // Hilt Extensions
    def hilt_ext = "1.0.0"
    implementation "androidx.hilt:hilt-navigation-fragment:$hilt_ext"
    implementation "androidx.hilt:hilt-work:$hilt_ext"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "androidx.hilt:hilt-compiler:$hilt_ext"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.11.0"

    // Picasso
    implementation "com.squareup.picasso:picasso:2.71828"

    // Work Manager
    def work = "2.5.0"
    implementation "androidx.work:work-runtime-ktx:$work"
    androidTestImplementation "androidx.work:work-testing:$work"

    // DataStore
    implementation "androidx.datastore:datastore:1.0.0-beta01"
    implementation  "com.google.protobuf:protobuf-javalite:3.10.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0-beta01"


    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Unit Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.robolectric:robolectric:4.5.1"

    // Coroutines Testing
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines"

    // Instrumented Unit Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    // Google Truth for Test Assertions
    def truth = "1.1.2"
    testImplementation "com.google.truth:truth:$truth"
    androidTestImplementation "com.google.truth:truth:$truth"

    // Testing with Hilt
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_ver"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_ver"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}